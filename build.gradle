buildscript {
    ext {
        compose_version = '1.4.0-beta02'
        hilt_version = '2.44'
        ktlintVersion = '11.1.0'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "com.google.code.gson:gson:2.9.0"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintVersion"
    }
}

plugins {
    id 'com.android.application' version '7.4.0' apply false
    id 'com.android.library' version '7.4.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id 'org.jetbrains.dokka' version "1.7.20"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.jetbrains.dokka"

    ktlint {
        verbose = true
        ignoreFailures = false

        reporters {
            reporter "plain"
            reporter "checkstyle"
        }

        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }

    afterEvaluate {
        if (tasks.findByName('dokkaHtml') == null) {
            return
        }

        tasks.named("dokkaHtml") {
            offlineMode.set(true)

            dokkaSourceSets.configureEach {
                includes.from("Packages.md")

                skipEmptyPackages.set(true)

                perPackageOption {
                    matchingRegex.set(".*.(presentation|core|data|util|di|ui).*")
                    suppress.set(true)
                }
            }
        }
    }
}